# Maintainer: Reza Jahanbakhshi <reza.jahanbakhshi at gmail dot com
# Contributor: Lone_Wolf <lone_wolf@klaas-de-kat.nl>
# Contributor: Armin K. <krejzi at email dot com>
# Contributor: Kristian Klausen <klausenbusk@hotmail.com>
# Contributor: Egon Ashrafinia <e.ashrafinia@gmail.com>
# Contributor: Tavian Barnes <tavianator@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Antti "Tera" Oja <antti.bofh@gmail.com>
# Contributor: Diego Jose <diegoxter1006@gmail.com>

pkgname=mesa-git
pkgdesc="an open-source implementation of the OpenGL specification, git version"
pkgver=24.3.0_devel.192894.66243e6999f.d41d8cd
pkgrel=1
arch=('x86_64')
makedepends=('git'
             'python-mako'
             'xorgproto'
             'libxml2'
             'libva'
             'libxrandr'
             'wayland-protocols'
             'meson'
             'glslang'
             'vulkan-icd-loader'
             'libglvnd'
             'python-packaging'
             'python-yaml'
             'llvm'
             'clang')
depends=('libdrm'
         'libxxf86vm'
         'libxshmfence'
         'libelf'
         'wayland'
         'lm_sensors'
         'zstd'
         'expat'
         'gcc-libs'
         'libxfixes'
         'libx11'
         'systemd-libs'
         'libxext'
         'glibc'
         'zlib'
         'llvm-libs'
         'clang')
optdepends=('opengl-man-pages: for the OpenGL API man pages')
provides=('mesa'
          'vulkan-radeon'
          'vulkan-mesa-layers'
          'libva-mesa-driver'
          'vulkan-driver'
          'mesa-libgl'
          'opengl-driver')
conflicts=('mesa'
           'opencl-clover-mesa'
           'opencl-rusticl-mesa'
           'vulkan-intel'
           'vulkan-radeon'
           'vulkan-mesa-layers'
           'libva-mesa-driver'
           'mesa-vdpau'
           'vulkan-swrast'
           'mesa-libgl')
url="https://www.mesa3d.org"
license=('custom')
source=('mesa::git+https://gitlab.freedesktop.org/mesa/mesa.git#branch=main'
             'LICENSE'
)
md5sums=('SKIP'
         '5c65a0fe315dd347e09b1f2826a1df5a')
sha512sums=('SKIP'
            '25da77914dded10c1f432ebcbf29941124138824ceecaf1367b3deedafaecabc082d463abcfa3d15abff59f177491472b505bcb5ba0c4a51bb6b93b4721a23c2')
options=(!debug)

pkgver() {
    cd mesa


    local _ver
    _ver=$(<VERSION)

    local _patchver
    local _patchfile
    for _patchfile in "${source[@]}"; do
        _patchfile="${_patchfile%%::*}"
        _patchfile="${_patchfile##*/}"
        [[ $_patchfile = *.patch ]] || continue
        _patchver="${_patchver}$(md5sum ${srcdir}/${_patchfile} | cut -c1-32)"
    done
    _patchver="$(echo -n $_patchver | md5sum | cut -c1-7)"

    echo ${_ver/-/_}.$(git rev-list --count HEAD).$(git rev-parse --short HEAD).${_patchver}
}

prepare() {
    # although removing build folder in build() function feels more natural,
    # that interferes with the spirit of makepkg --noextract
    if [  -d build ]; then
        rm -rf build
    fi

    local _patchfile
    for _patchfile in "${source[@]}"; do
        _patchfile="${_patchfile%%::*}"
        _patchfile="${_patchfile##*/}"
        [[ $_patchfile = *.patch ]] || continue
        echo "Applying patch $_patchfile..."
        patch --directory=mesa --forward --strip=1 --input="${srcdir}/${_patchfile}"
    done
}

build () {
      local meson_options=(
       -D android-libbacktrace=disabled
       -D b_ndebug=true
       -D b_lto=true
       -D platforms=x11,wayland
       -D gallium-drivers=radeonsi,zink
       -D vulkan-drivers=amd
       -D vulkan-layers=device-select,overlay
       -D gallium-extra-hud=true
       -D gallium-nine=false
       -D gallium-opencl=disabled
       -D gles1=disabled
       -D glx=dri
       -D libunwind=disabled
       -D osmesa=false
       -D microsoft-clc=disabled
       -D valgrind=disabled
       -D video-codecs=all
       -D gallium-xa=disabled
       -D gallium-vdpau=disabled
 )

  # Inject subproject packages
  export MESON_PACKAGE_CACHE_DIR="$srcdir"

  arch-meson mesa build "${meson_options[@]}"
  meson configure build --no-pager # Print config
  meson compile -C build

}
package() {
  meson install -C build --destdir "$pkgdir"

  local libdir=usr/lib

  cd $pkgdir

  local libdir=usr/lib

    # remove script file from /usr/bin
    # https://gitlab.freedesktop.org/mesa/mesa/issues/2230
    rm "${pkgdir}/usr/bin/mesa-overlay-control.py"
    rmdir "${pkgdir}/usr/bin"

    # indirect rendering
    ln -sr $libdir/libGLX_{mesa,indirect}.so.0

    install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" "${srcdir}/LICENSE"
}
